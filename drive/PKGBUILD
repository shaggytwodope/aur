# Maintainer: John Jenkins <twodopeshaggy@gmail.com>

pkgname=drive
pkgver=0.0.8
pkgrel=1
pkgdesc="Pull or push Google Drive files"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
url="http://github.com/odeke-em/drive"
license=('Apache')
makedepends=('go' 'git' 'mercurial')
options=('!strip' '!emptydirs')
source=("https://github.com/odeke-em/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('5625aa9c71f49beb859efd27f6a2e2041efcdc285b475a48accac245dcc172ac')

prepare() {
 mkdir -p "$srcdir/go"
 export GOPATH="$srcdir/go"
 go get github.com/odeke-em/drive/cmd/drive
 go get github.com/odeke-em/drive/config
 go get github.com/rakyll/command
}

build() {
  export GOPATH="$srcdir/go"
  cd "$pkgname-$pkgver/cmd/drive"

  msg2 'Building drive'
  go build -o "$pkgname" main.go

}

package() {
  cd "$pkgname-$pkgver/cmd/drive"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

}
# vim:set ts=2 sw=2 et:
