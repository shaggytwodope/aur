# Maintainer: John Jenkins <twodopeshaggy@gmail.com>

pkgname=drive
pkgver=0.0.5
pkgrel=1
pkgdesc="Pull or push Google Drive files"
arch=('x86_64' 'i686')
url="http://github.com/odeke-em/drive"
license=('Apache')
makedepends=('go' 'git' 'mercurial')
options=('!strip' '!emptydirs')
source=("https://github.com/odeke-em/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('2645994e29bd128b19ea5296742f4c5c0468b4c4d76bda93cc547c7acd8180c6')

prepare() {
 mkdir -p "$srcdir/go"
 export GOPATH="$srcdir/go"
 go get github.com/odeke-em/drive/cmd/drive
 go get github.com/odeke-em/drive/config
 go get github.com/rakyll/command
}

build() {
  export GOPATH="$srcdir/go"
  cd "$pkgname-$pkgver/cmd/drive"

  msg2 'Building drive'
  go build -o "$pkgname" main.go

}

package() {
  cd "$pkgname-$pkgver/cmd/drive"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

}
# vim:set ts=2 sw=2 et:
