# Maintainer: John Jenkins <twodopeshaggy@gmail.com>

pkgname=drive
pkgver=0.1.0
pkgrel=1
pkgdesc="Pull or push Google Drive files"
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
url="http://github.com/odeke-em/drive"
license=('Apache')
makedepends=('go' 'git' 'mercurial')
options=('!strip' '!emptydirs')
source=("https://github.com/odeke-em/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('93f0c17d6a34fae1972e1d4c1b7eccc45c822bc1c0c99479c11787a38617c8f3')

prepare() {
 mkdir -p "$srcdir/go"
 export GOPATH="$srcdir/go"
 go get github.com/odeke-em/drive/cmd/drive
 go get github.com/odeke-em/drive/config
 go get github.com/rakyll/command
}

build() {
  export GOPATH="$srcdir/go"
  cd "$pkgname-$pkgver/cmd/drive"

  msg2 'Building drive'
  go build -o "$pkgname" main.go

}

package() {
  cd "$pkgname-$pkgver/cmd/drive"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

}
# vim:set ts=2 sw=2 et:
